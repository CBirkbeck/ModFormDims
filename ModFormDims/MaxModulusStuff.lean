import Mathlib.Analysis.Complex.AbsMax
import Mathlib.NumberTheory.Modular
import Mathlib.NumberTheory.ModularForms.Basic
import Mathlib.NumberTheory.ModularForms.CongruenceSubgroups
import ModFormDims.QExpansionModform

/-!
# Maximum-modulus criteria for functions to be constant
-/

open Complex

/--
If `f` is differentiable on `{z : ‚ÑÇ | ‚Äñz‚Äñ < 1}`, and `‚Äñf‚Äñ` attains a maximum in this open ball,
then `f` is constant.
-/
lemma eq_const_of_exists_max {f : ‚ÑÇ ‚Üí ‚ÑÇ} (h_an : DifferentiableOn ‚ÑÇ f {z : ‚ÑÇ | ‚Äñz‚Äñ < 1})
    {v : ‚ÑÇ} (hv : ‚Äñv‚Äñ < 1) (hv_max : ‚àÄ z, ‚Äñz‚Äñ < 1 ‚Üí ‚Äñf z‚Äñ ‚â§ ‚Äñf v‚Äñ) :
    Set.EqOn f (Function.const ‚ÑÇ (f v)) {z : ‚ÑÇ | ‚Äñz‚Äñ < 1} := by
  refine Complex.eqOn_of_isPreconnected_of_isMaxOn_norm ?_ ?_ h_an hv hv_max
  ¬∑ apply Convex.isPreconnected
    simpa only [ball_zero_eq] using convex_ball (0 : ‚ÑÇ) 1
  ¬∑ simpa only [‚Üê ball_zero_eq] using Metric.isOpen_ball

/--
If `f` is a function differentiable on the open unit ball, and there exists an `r < 1` such that
any value of `‚Äñf‚Äñ` on the open ball is bounded above by some value on the closed ball of radius `r`,
then `f` is constant.
-/
lemma eq_const_of_exists_le {f : ‚ÑÇ ‚Üí ‚ÑÇ} (h_an : DifferentiableOn ‚ÑÇ f {z : ‚ÑÇ | ‚Äñz‚Äñ < 1})
    {r : ‚Ñù} (hr_nn : 0 ‚â§ r) (hr_lt : r < 1)
    (hr : ‚àÄ z, ‚Äñz‚Äñ < 1 ‚Üí ‚àÉ w, ‚Äñw‚Äñ ‚â§ r ‚àß ‚Äñf z‚Äñ ‚â§ ‚Äñf w‚Äñ) :
    Set.EqOn f (Function.const ‚ÑÇ (f 0)) {z : ‚ÑÇ | ‚Äñz‚Äñ < 1} := by
  let V : Set ‚ÑÇ := {z | ‚Äñz‚Äñ ‚â§ r}
  have hVc : IsCompact V := by simpa only [norm_eq_abs, Metric.closedBall, dist_eq_norm_sub,
    sub_zero, V] using isCompact_closedBall (0 : ‚ÑÇ) r
  have hVne : V.Nonempty := ‚ü®0, by simp only [norm_eq_abs, Set.mem_setOf_eq, map_zero, hr_nn, V]‚ü©
  have : ContinuousOn f V := by
    apply h_an.continuousOn.mono
    simpa only [norm_eq_abs, Set.setOf_subset_setOf, V] using fun _ ha ‚Ü¶ ha.trans_lt hr_lt
  obtain ‚ü®x, hx_mem, hx_max‚ü© := hVc.exists_isMaxOn hVne this.norm
  suffices Set.EqOn f (Function.const ‚ÑÇ (f x)) {z : ‚ÑÇ | ‚Äñz‚Äñ < 1} by
    refine this.trans fun y _ ‚Ü¶ ?_
    simp only [Function.const_apply, this (by simp only [norm_zero, zero_lt_one] : ‚Äñ(0 : ‚ÑÇ)‚Äñ < 1)]
  apply eq_const_of_exists_max h_an (lt_of_le_of_lt hx_mem hr_lt) (fun z hz ‚Ü¶ ?_)
  obtain ‚ü®w, hw, hw'‚ü© := hr z hz
  exact hw'.trans (hx_max hw)

/-
We want to apply the above lemma to `cusp_function h f`, where `f` is a modular form of weight 0.
If we choose `r = exp (-œÄ * ‚àö3)`, then the image of the fundamental domain is contained in
`{z | ‚Äñz‚Äñ ‚â§ r}`.
-/

open UpperHalfPlane Modular
local notation "SL(" n ", " R ")" => Matrix.SpecialLinearGroup (Fin n) R

/-- non-strict variant of `ModularGroup.three_le_four_mul_im_sq_of_mem_fdo` -/
theorem ModularGroup.three_le_four_mul_im_sq_of_mem_fd {œÑ : ‚Ñç} (h : œÑ ‚àà ùíü) : 3 ‚â§ 4 * œÑ.im ^ 2 := by
  have : 1 ‚â§ œÑ.re * œÑ.re + œÑ.im * œÑ.im := by simpa [Complex.normSq_apply] using h.1
  have := h.2
  cases abs_cases œÑ.re <;> nlinarith

lemma exists_translate (œÑ : ‚Ñç) :
    ‚àÉ Œ≥ : SL(2, ‚Ñ§), 1 / 2 ‚â§ im (Œ≥ ‚Ä¢ œÑ) := by
  obtain ‚ü®Œ≥, hŒ≥‚ü© := ModularGroup.exists_smul_mem_fd œÑ
  use Œ≥
  have := ModularGroup.three_le_four_mul_im_sq_of_mem_fd hŒ≥
  have := UpperHalfPlane.im_pos (Œ≥ ‚Ä¢ œÑ)
  nlinarith

lemma denom_one (œÑ : ‚Ñç) : denom 1 œÑ = 1 := by
  simp only [denom, OneMemClass.coe_one, Fin.isValue, Units.val_one, ne_eq, one_ne_zero,
    not_false_eq_true, Matrix.one_apply_ne, ofReal_zero, zero_mul, Matrix.one_apply_eq, ofReal_one,
    zero_add, norm_one]

lemma ModularGroup.coe_one : UpperHalfPlane.ModularGroup.coe' 1 = 1 := by
  simp only [ModularGroup.coe', map_one]

lemma exists_translate' (œÑ : ‚Ñç) :
    ‚àÉ Œ≥ : SL(2, ‚Ñ§), 1 / 2 ‚â§ im (Œ≥ ‚Ä¢ œÑ) ‚àß ‚Äñdenom Œ≥ œÑ‚Äñ ‚â§ 1 := by
  by_cases h : 1 / 2 ‚â§ œÑ.im
  ¬∑ refine ‚ü®1, by simpa using h, by simp [ModularGroup.coe_one, denom_one]‚ü©
  ¬∑ obtain ‚ü®Œ≥, hŒ≥‚ü© := exists_translate œÑ
    refine ‚ü®Œ≥, hŒ≥, ?_‚ü©
    have h0 := im_smul_eq_div_normSq Œ≥ œÑ
    simp only [ModularGroup.det_coe', one_mul, ‚Üê UpperHalfPlane.ModularGroup.sl_moeb] at h0
    have h1 : œÑ.im ‚â§ (Œ≥ ‚Ä¢ œÑ).im := by nlinarith
    rw [h0, le_div_iff‚ÇÄ (normSq_denom_pos (‚ÜëŒ≥) œÑ), normSq_eq_norm_sq] at h1
    have H : ‚Äñdenom Œ≥ œÑ‚Äñ^2 ‚â§ 1 := (mul_le_iff_le_one_right œÑ.2).mp h1
    simpa using H

def coe1 : SL(2, ‚Ñ§) ‚Üí Œì :=
  fun g => ‚ü®‚Üëg, by simp [Œì, CongruenceSubgroup.Gamma_one_top]‚ü©

instance : Coe SL(2, ‚Ñ§) Œì := ‚ü®coe1‚ü©

@[simp]
lemma coe_smul_eq_smul {g : SL(2, ‚Ñ§)} {œÑ : ‚Ñç} : (g : Œì) ‚Ä¢ œÑ = (g ‚Ä¢ œÑ) := by
  simp only [coe1, Subgroup.mk_smul, ModularGroup.sl_moeb]

@[simp]
lemma denom_coe1_eq_denom {g : SL(2, ‚Ñ§)} {œÑ : ‚Ñç} : denom (g : Œì) œÑ = denom g œÑ := by
  simp only [denom, coe1, Fin.isValue, ModularGroup.coe'_apply_complex]

theorem slash_action_eqn'' {F : Type*} [FunLike F ‚Ñç ‚ÑÇ] (k : ‚Ñ§) (Œì : Subgroup SL(2, ‚Ñ§))
    [SlashInvariantFormClass F Œì k] (f : F) (Œ≥ : Œì) (z : ‚Ñç) :
    f (Œ≥ ‚Ä¢ z) = (denom Œ≥ z) ^ k * f z := by
  rw [denom]
  exact (SlashInvariantForm.slash_action_eqn' k Œì f Œ≥ z)

lemma modform_exists_norm_le {k : ‚Ñ§} (hk : k ‚â§ 0) {F : Type*} [FunLike F ‚Ñç ‚ÑÇ]
    [ModularFormClass F Œì k] (f : F)  (œÑ : ‚Ñç) :
    ‚àÉ Œæ : ‚Ñç, 1/2 ‚â§ Œæ.im ‚àß ‚Äñf œÑ‚Äñ ‚â§ ‚Äñf Œæ‚Äñ := by
    /- Proof: take Œæ = Œ≥ ‚Ä¢ œÑ where Œ≥ is as in `exists_translate'`. Then use the equation
  `f Œæ = (denom Œ≥ œÑ) ^ k * f œÑ` and the fact that `k ‚â§ 0` and `‚Äñdenom Œ≥ œÑ‚Äñ ‚â§ 1`.
  -/
  obtain ‚ü®Œ≥, hŒ≥, hdenom‚ü© := exists_translate' œÑ
  use Œ≥ ‚Ä¢ œÑ
  refine ‚ü®hŒ≥, ?_‚ü©
  have := slash_action_eqn'' k Œì f Œ≥ œÑ
  rw [coe_smul_eq_smul, denom_coe1_eq_denom] at this
  rw [this,norm_mul, norm_zpow]
  have h2 : 0 ‚â§ ‚Äñf œÑ‚Äñ := norm_nonneg (f œÑ)
  have h3 : 1 ‚â§ ‚Äñdenom (Œ≥ : SL(2, ‚Ñ§)) œÑ‚Äñ ^ k := by
    apply one_le_zpow_of_nonpos‚ÇÄ _ hdenom hk
    rw [norm_pos_iff]
    apply denom_ne_zero
  nlinarith
open ModularForm CongruenceSubgroup

/- local notation "GL(" n ", " R ")" "‚Å∫" => Matrix.GLPos (Fin n) R

lemma aux (A B : SL(2, ‚Ñ§)) : (ModularGroup.coe' A) * B = ModularGroup.coe' (A * B) := by
  simp_rw [ModularGroup.coe']
  simp only [map_mul]

lemma aux2  : (ModularGroup.coe' 1)  = 1 := by
  simp_rw [ModularGroup.coe']
  simp only [map_one] -/

lemma slash_eq_func_prod (f : ‚Ñç ‚Üí ‚ÑÇ) (k : ‚Ñ§) (H : Subgroup SL(2, ‚Ñ§)) (Œ≥ : H) : f ‚à£[k] Œ≥ =
    (fun z => f (Œ≥ ‚Ä¢ z)) * (fun z => (denom Œ≥ z)^k)‚Åª¬π := by
  ext z
  simp [slash_def, slash]

@[simp]
lemma denom_S (z : ‚Ñç) : denom (ModularGroup.S) z = z.1 := by
  rw [ModularGroup.S, denom]
  simp only [Int.reduceNeg, Fin.isValue, ModularGroup.coe'_apply_complex, Matrix.of_apply,
    Matrix.cons_val', Matrix.cons_val_zero, Matrix.empty_val', Matrix.cons_val_fin_one,
    Matrix.cons_val_one, Matrix.head_fin_const, Int.cast_one, one_mul, Matrix.head_cons,
    Int.cast_zero, add_zero]
  rfl

lemma Complex.zpow_two_eq_one (k : ‚Ñ§) (h : (2 : ‚ÑÇ) ^ k = 1) : k = 0 := by
  replace h : ‚Äñ(2 : ‚ÑÇ)^k‚Äñ = 1 := by simp [h]
  replace h : ‚Äñ(2 : ‚Ñù)^k‚Äñ = 1 := by simp [‚Üê h]
  replace h : (2 : ‚Ñù)^k = (2 : ‚Ñù)^(0 : ‚Ñ§) := by simp [‚Üê h]
  exact zpow_right_injective‚ÇÄ (by norm_num) (by norm_num) h


lemma const_modform_neg_wt_eq_zero_lvl_one {F : Type*} [FunLike F ‚Ñç ‚ÑÇ] (k : ‚Ñ§)
    [ModularFormClass F Œì k] (f : F) (c : ‚ÑÇ) (hf : ‚áëf = (fun _ => c)) : k = 0 ‚à® c = 0 := by
  have := slash_action_eqn'' k Œì f
  rw [hf] at this
  have hI := (this ModularGroup.S) I
  have h2I2 := (this ModularGroup.S) ‚ü®2 * Complex.I, by simp‚ü©
  simp only [SlashInvariantForm.toFun_eq_coe, toSlashInvariantForm_coe, subgroup_slash,
    Subtype.forall, Gamma_mem, Fin.isValue, and_imp, denom_coe1_eq_denom, denom_S, Pi.mul_apply,
    Pi.inv_apply] at *
  nth_rw 1 [ hI] at h2I2
  simp  [mul_eq_mul_left_iff, inv_inj] at h2I2
  rcases h2I2 with H | H
  ¬∑ left
    symm at H
    rw [UpperHalfPlane.I, mul_zpow, mul_left_eq_self‚ÇÄ] at H
    rcases H with H | H
    ¬∑ apply Complex.zpow_two_eq_one k H
    ¬∑ exfalso
      exact (zpow_ne_zero k I_ne_zero) H
  ¬∑ exact Or.inr H

open Real

lemma neg_wt_modform_zero (k : ‚Ñ§) (hk : k ‚â§ 0) {F : Type*} [FunLike F ‚Ñç ‚ÑÇ]
    [ModularFormClass F Œì k] (f : F) : ‚áëf = 0 ‚à® (k = 0 ‚àß ‚àÉ c : ‚ÑÇ, ‚áëf = fun _ => c) := by
  have hdiff : DifferentiableOn ‚ÑÇ (cuspFcnH f) {z : ‚ÑÇ | ‚Äñz‚Äñ < 1} := by
    exact fun z hz ‚Ü¶ DifferentiableAt.differentiableWithinAt (cusp_fcn_diff f hz)
  have heq : Set.EqOn (cuspFcnH f) (Function.const ‚ÑÇ ((cuspFcnH f) 0)) {z : ‚ÑÇ | ‚Äñz‚Äñ < 1} := by
    apply eq_const_of_exists_le (r := exp (-(œÄ * ‚àö3)/2)) hdiff
    ¬∑ exact exp_nonneg _
    ¬∑ refine exp_lt_one_iff.mpr ?hr_lt.a
      rw [@div_neg_iff]
      right
      simp only [Left.neg_neg_iff, pi_pos, mul_pos_iff_of_pos_left, sqrt_pos, Nat.ofNat_pos,
        and_self]
    ¬∑ intro z hz
      rcases eq_or_ne z 0 with rfl | hz'
      ¬∑ refine ‚ü®0, by simpa using exp_nonneg _, by rfl‚ü©
      ¬∑ obtain ‚ü®Œæ, hŒæ, hŒæ‚ÇÇ‚ü© := modform_exists_norm_le hk f ‚ü®(Z 1 z), by apply z_in_H hz hz'‚ü©
        use Q 1 Œæ
        constructor
        ¬∑ rw [Q]
          simp only [ofReal_one, div_one, Complex.norm_eq_abs, Complex.abs_exp]
          gcongr
          simp only [mul_re, re_ofNat, ofReal_re, im_ofNat, ofReal_im, mul_zero, sub_zero,
            Complex.I_re, mul_im, zero_mul, add_zero, Complex.I_im, mul_one, sub_self, coe_re,
            coe_im, zero_sub, neg_le]
          ring_nf
          simp_rw [mul_assoc]
          apply mul_le_mul_of_nonneg_left _ pi_nonneg
          have : 1 ‚â§ Œæ.im * 2 := by
            rw [div_le_iff‚ÇÄ zero_lt_two] at hŒæ
            exact hŒæ
          apply le_trans _ this
          have : ‚àö3 ‚â§ 2 := by
            apply sqrt_le_iff.mpr ?_
            norm_cast
          linarith
        ¬∑ rw [eq_cuspFcnH f Œæ, eq_cuspFcnH f ‚ü®(Z 1 z), by apply z_in_H hz hz'‚ü©] at hŒæ‚ÇÇ
          convert hŒæ‚ÇÇ
          exact (QZ_eq_id one_ne_zero z hz').symm
  have H : ‚áëf = Function.const ‚Ñç ((cuspFcnH f) 0) := by
    ext z
    have e1 := eq_cuspFcnH f z
    have hQ : Q 1 z ‚àà {z | ‚Äñz‚Äñ < 1} := by
      simpa using (abs_q_lt_iff zero_lt_one 0 z.1).mpr z.2
    simpa only [SlashInvariantForm.toFun_eq_coe, toSlashInvariantForm_coe, e1,
      Function.const_apply] using heq hQ
  have HF := const_modform_neg_wt_eq_zero_lvl_one k f (cuspFcnH f 0) H
  rcases HF with HF | HF
  ¬∑ right
    simp [HF]
    use (cuspFcnH (‚áëf) 0)
    simpa using H
  ¬∑ left
    ext z
    have := congrFun H z
    rw [HF] at this
    simpa only [zero_apply, SlashInvariantForm.toFun_eq_coe, toSlashInvariantForm_coe,
      Function.const_apply] using this


lemma ModularForm_neg_weigth_eq_zero (k : ‚Ñ§) (hk : k < 0) {F : Type*} [FunLike F ‚Ñç ‚ÑÇ]
    [ModularFormClass F Œì k] (f : F) : ‚áëf = 0 := by
  rcases neg_wt_modform_zero k hk.le f with h | ‚ü®rfl, _, _‚ü©
  exact h
  aesop

lemma ModularForm_weight_zero_constant {F : Type*} [FunLike F ‚Ñç ‚ÑÇ]
    [ModularFormClass F Œì 0] (f : F) : ‚àÉ c : ‚ÑÇ, ‚áëf = fun _ => c := by
  rcases neg_wt_modform_zero 0 (by rfl) f with h1 | h2
  refine ‚ü®0, ?_‚ü©
  simp only [h1, SlashInvariantForm.toFun_eq_coe, toSlashInvariantForm_coe, coe_zero]
  rfl
  aesop

lemma weigth_zero_rank_eq_one : Module.rank ‚ÑÇ (ModularForm Œì 0) = 1 := by
  let f := ModularForm.const 1 (Œì := Œì)
  have hf : f ‚â† 0 := by
    rw [@DFunLike.ne_iff]
    use I
    simp only [const_toFun, Function.const_apply, zero_apply, ne_eq, one_ne_zero, not_false_eq_true,
      f]
  apply rank_eq_one f hf
  intro g
  rw [@DFunLike.ne_iff] at hf
  obtain ‚ü®c', hc'‚ü© := ModularForm_weight_zero_constant g
  use c'
  ext z
  simp only [zero_apply, ne_eq, SlashInvariantForm.toFun_eq_coe, toSlashInvariantForm_coe,
    smul_apply, smul_eq_mul] at *
  have : f z = 1 := rfl
  simp [this]
  exact Eq.symm (congrFun hc' z)

lemma neg_weight_rank_zero (k : ‚Ñ§) (hk : k < 0) : Module.rank ‚ÑÇ (ModularForm Œì k) = 0 := by
  rw [rank_eq_zero_iff]
  intro f
  refine ‚ü®1, by simp, ?_‚ü©
  exact
    Eq.mpr (id (congrArg (fun x ‚Ü¶ x = 0) (one_smul ‚ÑÇ f)))
      (ModularForm.ext_iff.mpr (congrFun (ModularForm_neg_weigth_eq_zero k hk f)))

-- Now, if we can get the `cusp function` stuff from QExpansion.lean working properly, we can
-- deduce that any level 1, wt ‚â§ 0 modular form is constant.
-- Clearly a nonzero constant can't be modular of weight < 0 -- we should probably have a lemma
-- that a function which is modular for the same group in two different weights is 0 --
-- so we are done.
