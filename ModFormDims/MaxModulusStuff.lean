import Mathlib.Analysis.Complex.AbsMax
import Mathlib.NumberTheory.Modular
import Mathlib.NumberTheory.ModularForms.Basic
import Mathlib.NumberTheory.ModularForms.CongruenceSubgroups

/-!
# Maximum-modulus criteria for functions to be constant
-/

open Complex

/--
If `f` is differentiable on `{z : â„‚ | â€–zâ€– < 1}`, and `â€–fâ€–` attains a maximum in this open ball,
then `f` is constant.
-/
lemma eq_const_of_exists_max {f : â„‚ â†’ â„‚} (h_an : DifferentiableOn â„‚ f {z : â„‚ | â€–zâ€– < 1})
    {v : â„‚} (hv : â€–vâ€– < 1) (hv_max : âˆ€ z, â€–zâ€– < 1 â†’ â€–f zâ€– â‰¤ â€–f vâ€–) :
    Set.EqOn f (Function.const â„‚ (f v)) {z : â„‚ | â€–zâ€– < 1} := by
  refine Complex.eqOn_of_isPreconnected_of_isMaxOn_norm ?_ ?_ h_an hv hv_max
  Â· apply Convex.isPreconnected
    simpa only [ball_zero_eq] using convex_ball (0 : â„‚) 1
  Â· simpa only [â† ball_zero_eq] using Metric.isOpen_ball

/--
If `f` is a function differentiable on the open unit ball, and there exists an `r < 1` such that
any value of `â€–fâ€–` on the open ball is bounded above by some value on the closed ball of radius `r`,
then `f` is constant.
-/
lemma eq_const_of_exists_le {f : â„‚ â†’ â„‚} (h_an : DifferentiableOn â„‚ f {z : â„‚ | â€–zâ€– < 1})
    {r : â„} (hr_nn : 0 â‰¤ r) (hr_lt : r < 1)
    (hr : âˆ€ z, â€–zâ€– < 1 â†’ âˆƒ w, â€–wâ€– â‰¤ r âˆ§ â€–f zâ€– â‰¤ â€–f wâ€–) :
    Set.EqOn f (Function.const â„‚ (f 0)) {z : â„‚ | â€–zâ€– < 1} := by
  let V : Set â„‚ := {z | â€–zâ€– â‰¤ r}
  have hVc : IsCompact V := by simpa only [norm_eq_abs, Metric.closedBall, dist_eq_norm_sub,
    sub_zero, V] using isCompact_closedBall (0 : â„‚) r
  have hVne : V.Nonempty := âŸ¨0, by simp only [norm_eq_abs, Set.mem_setOf_eq, map_zero, hr_nn, V]âŸ©
  have : ContinuousOn f V := by
    apply h_an.continuousOn.mono
    simpa only [norm_eq_abs, Set.setOf_subset_setOf, V] using fun _ ha â†¦ ha.trans_lt hr_lt
  obtain âŸ¨x, hx_mem, hx_maxâŸ© := hVc.exists_isMaxOn hVne this.norm
  suffices Set.EqOn f (Function.const â„‚ (f x)) {z : â„‚ | â€–zâ€– < 1} by
    refine this.trans fun y _ â†¦ ?_
    simp only [Function.const_apply, this (by simp only [norm_zero, zero_lt_one] : â€–(0 : â„‚)â€– < 1)]
  apply eq_const_of_exists_max h_an (lt_of_le_of_lt hx_mem hr_lt) (fun z hz â†¦ ?_)
  obtain âŸ¨w, hw, hw'âŸ© := hr z hz
  exact hw'.trans (hx_max hw)

/-
We want to apply the above lemma to `cusp_function h f`, where `f` is a modular form of weight 0.
If we choose `r = exp (-Ï€ * âˆš3)`, then the image of the fundamental domain is contained in
`{z | â€–zâ€– â‰¤ r}`.
-/

open UpperHalfPlane Modular
local notation "SL(" n ", " R ")" => Matrix.SpecialLinearGroup (Fin n) R

/-- non-strict variant of `ModularGroup.three_le_four_mul_im_sq_of_mem_fdo` -/
theorem ModularGroup.three_le_four_mul_im_sq_of_mem_fd {Ï„ : â„} (h : Ï„ âˆˆ ğ’Ÿ) : 3 â‰¤ 4 * Ï„.im ^ 2 := by
  have : 1 â‰¤ Ï„.re * Ï„.re + Ï„.im * Ï„.im := by simpa [Complex.normSq_apply] using h.1
  have := h.2
  cases abs_cases Ï„.re <;> nlinarith

lemma exists_translate (Ï„ : â„) :
    âˆƒ Î³ : SL(2, â„¤), 1 / 2 â‰¤ im (Î³ â€¢ Ï„) := by
  obtain âŸ¨Î³, hÎ³âŸ© := ModularGroup.exists_smul_mem_fd Ï„
  use Î³
  have := ModularGroup.three_le_four_mul_im_sq_of_mem_fd hÎ³
  have := UpperHalfPlane.im_pos (Î³ â€¢ Ï„)
  nlinarith

abbrev Î“ := CongruenceSubgroup.Gamma 1

lemma denom_one (Ï„ : â„) : denom 1 Ï„ = 1 := by
  simp only [denom, OneMemClass.coe_one, Fin.isValue, Units.val_one, ne_eq, one_ne_zero,
    not_false_eq_true, Matrix.one_apply_ne, ofReal_zero, zero_mul, Matrix.one_apply_eq, ofReal_one,
    zero_add, norm_one]

lemma ModularGroup.coe_one : UpperHalfPlane.ModularGroup.coe' 1 = 1 := by
  simp only [ModularGroup.coe', map_one]

lemma exists_translate' (Ï„ : â„) :
    âˆƒ Î³ : SL(2, â„¤), 1 / 2 â‰¤ im (Î³ â€¢ Ï„) âˆ§ â€–denom Î³ Ï„â€– â‰¤ 1 := by
  by_cases h : 1 / 2 â‰¤ Ï„.im
  Â· refine âŸ¨1, by simpa using h, by simp [ModularGroup.coe_one, denom_one]âŸ©
  Â· obtain âŸ¨Î³, hÎ³âŸ© := exists_translate Ï„
    refine âŸ¨Î³, hÎ³, ?_âŸ©
    have h0 := im_smul_eq_div_normSq Î³ Ï„
    simp only [ModularGroup.det_coe', one_mul, â† UpperHalfPlane.ModularGroup.sl_moeb] at h0
    have h1 : Ï„.im â‰¤ (Î³ â€¢ Ï„).im := by nlinarith
    rw [h0, le_div_iffâ‚€ (normSq_denom_pos (â†‘Î³) Ï„), normSq_eq_norm_sq] at h1
    have H : â€–denom Î³ Ï„â€–^2 â‰¤ 1 := (mul_le_iff_le_one_right Ï„.2).mp h1
    simpa using H

def coe1 : SL(2, â„¤) â†’ Î“ :=
  fun g => âŸ¨â†‘g, by simp [Î“, CongruenceSubgroup.Gamma_one_top]âŸ©

instance : Coe SL(2, â„¤) Î“ := âŸ¨coe1âŸ©

@[simp]
lemma coe_smul_eq_smul {g : SL(2, â„¤)} {Ï„ : â„} : (g : Î“) â€¢ Ï„ =  (g â€¢ Ï„)  := by
  rw [coe1]
  simp

@[simp]
lemma denom_coe1_eq_denom {g : SL(2, â„¤)} {Ï„ : â„} : denom (g : Î“) Ï„ = denom g Ï„ := by
  simp only [denom, coe1, Fin.isValue, ModularGroup.coe'_apply_complex]

variable {F : Type*} [FunLike F â„ â„‚]

theorem slash_action_eqn'' (k : â„¤) (Î“ : Subgroup SL(2, â„¤)) [SlashInvariantFormClass F Î“ k] (f : F)
    (Î³ : Î“) (z : â„) : f (Î³ â€¢ z) = (denom Î³ z) ^ k * f z := by
  rw [denom]
  exact (SlashInvariantForm.slash_action_eqn' k Î“ f Î³ z)

lemma modform_exists_norm_le {k : â„¤} (hk : k â‰¤ 0) (f : ModularForm Î“ k) (Ï„ : â„) :
    âˆƒ Î¾ : â„, 1/2 â‰¤ Î¾.im âˆ§ â€–f Ï„â€– â‰¤ â€–f Î¾â€– := by
    /- Proof: take Î¾ = Î³ â€¢ Ï„ where Î³ is as in `exists_translate'`. Then use the equation
  `f Î¾ = (denom Î³ Ï„) ^ k * f Ï„` and the fact that `k â‰¤ 0` and `â€–denom Î³ Ï„â€– â‰¤ 1`.
  -/
  obtain âŸ¨Î³, hÎ³, hdenomâŸ© := exists_translate' Ï„
  use Î³ â€¢ Ï„
  refine âŸ¨hÎ³, ?_âŸ©
  have := slash_action_eqn'' k Î“ f Î³ Ï„
  rw [coe_smul_eq_smul, denom_coe1_eq_denom] at this
  rw [this,norm_mul, norm_zpow]
  have h2 : 0 â‰¤ â€–f Ï„â€– := norm_nonneg (f Ï„)
  have h3 : 1 â‰¤  â€–denom (Î³ : SL(2, â„¤)) Ï„â€– ^ k := by
    apply one_le_zpow_of_nonposâ‚€ _ hdenom hk
    rw [norm_pos_iff]
    apply denom_ne_zero
  nlinarith


-- Now, if we can get the `cusp function` stuff from QExpansion.lean working properly, we can
-- deduce that any level 1, wt â‰¤ 0 modular form is constant.
-- Clearly a nonzero constant can't be modular of weight < 0 -- we should probably have a lemma
-- that a function which is modular for the same group in two different levels is 0 --
-- so we are done.
